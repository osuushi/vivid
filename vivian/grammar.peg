{
// Grammar for the vivian row formatting language
package vivian
}

Root <- exprList:Expr+ EOF {
  return exprList, nil
} / Expr* CloseBrace {
  return nil, fmt.Errorf("Unexpected close brace")
}

Expr <- Content / Input / Text

Content <- tags:Tag+ OpenBrace children:Expr* CloseBrace {
  tagNames := makeStringSlice(tags)
  childNodes := children.([]interface{})
  var topNode *ContentNode
  for i := len(tagNames) - 1; i >= 0; i-- {
    topNode = &ContentNode{
      Tag: tagNames[i],
      Children: childNodes,
    }
    childNodes = []interface{}{topNode}
  }
	return topNode, nil
} / Tag+ !OpenBrace {
  return nil, fmt.Errorf("Expected open brace for tag. Escape the tag marker by doubling it, like @@.")
} / Tag+ OpenBrace .* !CloseBrace {
  return nil, fmt.Errorf("Expected close brace for tag.")
} / TagMarker OpenBrace {
  return nil, fmt.Errorf("Expected a tag name")
}

// Text requires escaping
Text <- chunks:( UnescapedChars / EscapeSequence )+ {
  chunkSlice := makeStringSlice(chunks)
  return &TextNode{
    Text: strings.Join(chunkSlice, ""),
  }, nil
}

Input <- TagMarker "-" nonFinalComponents:NonFinalInputComponent* finalComponent:FinalInputComponent SpaceChomper? {
  return &InputNode {
    Path: append(makeStringSlice(nonFinalComponents), finalComponent.(string)),
  }, nil
} / TagMarker "-" !AnyInputComponent {
  return nil, fmt.Errorf("Expected an input path")
} / TagMarker "-" NonFinalInputComponent+ "." {
  return nil, fmt.Errorf("Unexpected period in input path")
} / TagMarker "-" NonFinalInputComponent+ !FinalInputComponent {
  return nil, fmt.Errorf("Unexpected trailing period on input path.\nTip: End a sentence with an input using a chomp, like `@-foo~ .`")
}

SpaceChomper <- "~" _

AnyInputComponent "path component" <- NonFinalInputComponent / FinalInputComponent

NonFinalInputComponent "path component" <- name:Identifier '.' {
  return name.(string), nil
}

FinalInputComponent "last path component" <- name:Identifier !'.' {
  return name.(string), nil
}

EscapeSequence <- TagMarker escapedChar: EscapedChar {
  return string(escapedChar.([]byte)), nil
}

UnescapedChars <- (!EscapedChar .)+ {
  return string(c.text), nil
}

EscapedChar <- TagMarker / CloseBrace

Tag <- TagMarker tagName:Identifier {
  return tagName.(string), nil
}

Identifier <- [A-Z0-9]i+ {
  return string(c.text), nil
}

// Tags are normalized using ASCII control characters for start of header, start
// of text, and end of text, since it doesn't make sense to use these characters
// in text meant for display.
TagMarker <- '\x01'
OpenBrace <- '\x02'
CloseBrace <- '\x03'

_ "whitespace" â† [ \t\r\n]*
EOF <- !.
